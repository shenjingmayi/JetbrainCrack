@echo off
chcp 936 >nul
setlocal enabledelayedexpansion

:: 步骤1：检查Java环境
echo 正在检查Java环境...
java -version 2>nul
if %errorlevel% neq 0 (
    echo.
    echo [错误] Java未安装或未配置环境变量！
    echo 请安装JDK并设置JAVA_HOME环境变量后重试
    pause
    exit /b 1
)
for /f "tokens=3" %%j in ('java -version 2^>^&1 ^| findstr /i "version"') do (
    set "jver=%%j"
    set "jver=!jver:"=!"
)
echo 检测到Java版本: !jver!
echo.

:: 步骤2：用户输入
:input
set /p license_id=请输入License ID： 
set /p user_name=请输入User Name： 

:: 步骤3：执行并检查结果
echo 正在修补补丁ing...
java -jar sniarbtej-2024.2.8.jar -genkey -id=%license_id% -user=%user_name% > key.txt 2>nul

if not exist "key.txt" (
    echo 修补失败，未生成key文件
    goto input
)

for %%F in ("key.txt") do set size=%%~zF
if %size% EQU 0 (
    del "key.txt" >nul 2>&1
    echo.
    echo [错误] 修补失败，可能是输入的License ID和User Name的值存在特殊符号
    echo 请删去特殊符号后重新输入
    echo.
    goto input
)

echo.
echo 修补成功！生成的key文件已保存到key.txt
echo.

:: 步骤4：确认写入配置
:config
set /p choice=是否自动写入配置文件？(Y/N，不写入将无法使用):
if /i "!choice!"=="y" (
    goto write_config
) else if /i "!choice!"=="n" (
    echo 已跳过配置写入
    pause
    exit /b
) else (
    echo 请输入Y或N
    goto config
)

:: 步骤5：修改vmoptions文件
:write_config
echo 正在扫描.vmoptions文件...
set "jar_path=%~dp0sniarbtej-2024.2.8.jar"
set "jar_path=%jar_path:\=\\%"

for /r %%F in (*.vmoptions) do (
    if /i not "%%~xF"==".bak" (
        echo.
        echo 检测到%%~nxF文件，即将对其写入配置...
        
        :: 备份原文件
        copy "%%F" "%%F.bak" >nul
        
        :: 删除最后一行并添加新配置
        type "%%F" | findstr /v /i "javaagent" > "%%F.tmp"
        echo -javaagent:%~dp0sniarbtej-2024.2.8.jar >> "%%F.tmp"
        
        :: 替换原文件
        move /y "%%F.tmp" "%%F" >nul
        
        if exist "%%F" (
            echo 写入成功
            :: 删除自动生成的.bak文件
            del "%%F.bak" >nul 2>&1
        ) else (
            echo [错误] 写入失败
        )
        
        :: 删除临时文件
        del "%%F.tmp" >nul 2>&1
    )
)

echo.
echo 所有操作已完成！
pause